# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=kodi-addon-pvr-vdr-vnsi
pkgver=1.10.10
epoch=1
_gitver=550a4a2e093b4418394cdbe6f72cd7c76f0327b7
pkgrel=1
url="https://github.com/kodi-pvr/pvr.vdr.vnsi"
arch=('x86_64' 'i686' 'armv6h' 'armv7h')
license=('GPL2')
pkgdesc="PVR add-on for XBMC to add VDR as a TV/PVR Backend"
depends=('kodi')
makedepends=('cmake' 'git' 'libplatform')
source=("git+https://github.com/kodi-pvr/pvr.vdr.vnsi.git#commit=$_gitver"
        "git+https://github.com/xbmc/kodi-platform.git")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${srcdir}/pvr.vdr.vnsi/pvr.vdr.vnsi"
  grep '  version' addon.xml | cut -d'"' -f2
}

prepare() {
  mkdir -p build-addon build-platform
  cd "${srcdir}/pvr.vdr.vnsi"

  sed -i 's/@PLATFORM@/linux/g' pvr.vdr.vnsi/addon.xml
  sed -i 's/@LIBRARY_FILENAME@/pvr.vdr.vnsi.so/g' pvr.vdr.vnsi/addon.xml
  
  sed -i 's/p8-platform/platform/g' "$srcdir/kodi-platform/CMakeLists.txt"
}

build() {
  cd build-platform
  cmake -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_INSTALL_LIBDIR="/usr/lib" \
        -DCMAKE_INSTALL_LIBDIR_NOARCH="/usr/lib" \
        -DCMAKE_BUILD_TYPE=Release \
        ../kodi-platform

  cd ../build-addon

  cmake -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib/kodi \
        -Dkodiplatform_DIR="$srcdir/build-platform" \
        -DCMAKE_BUILD_TYPE=Release \
        ../pvr.vdr.vnsi

  make
}

package() {
  cd build-addon
  make DESTDIR="${pkgdir}" install

  cp -r "$srcdir"/pvr.vdr.vnsi/pvr.vdr.vnsi/* "$pkgdir/usr/lib/kodi/addons/pvr.vdr.vnsi"
}
